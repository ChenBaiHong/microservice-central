apply plugin: "org.springframework.boot"
apply plugin: "com.bmuschko.docker-spring-boot-application"

def archiveName = 'search-server'
def version = "${ver.baimicro.rootVersion}"

bootJar {
    archiveBaseName = archiveName
    archiveVersion = version
    manifest {
        attributes 'Start-Class': 'com.baimicro.central.SearchCenterApp'
    }
}

dependencies {
    implementation project(':central-config')
    implementation project(':search-center:search-client')
    implementation project(':component-integration:baimicro-common-spring-boot-starter')
    implementation project(':component-integration:baimicro-ribbon-spring-boot-starter')
    implementation(project(':component-integration:baimicro-elasticsearch-spring-boot-starter')) {
        exclude group: 'org.springframework.data', module: 'spring-data-elasticsearch'
    }
    implementation project(':component-integration:baimicro-swagger2-spring-boot-starter')

    implementation('com.alibaba.cloud:spring-cloud-starter-alibaba-nacos-discovery')
    implementation('org.springframework.cloud:spring-cloud-starter-openfeign')
    implementation('org.springframework.boot:spring-boot-starter-actuator')
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-security')
    implementation 'io.micrometer:micrometer-registry-prometheus'
    implementation libs['mybatis-plus-extension']

    compile("org.elasticsearch.client:elasticsearch-rest-high-level-client:$ver.elasticsearch.version")
    compile("org.elasticsearch.client:transport:$ver.elasticsearch.version")
    compile("org.elasticsearch.client:elasticsearch-rest-client:$ver.elasticsearch.version")
    compile("org.elasticsearch:elasticsearch:$ver.elasticsearch.version")
    compile("org.elasticsearch.plugin:transport-netty4-client:$ver.elasticsearch.version")
    compile(libs['spring-data-elasticsearch']) {
        exclude group: 'org.elasticsearch.client', module: 'elasticsearch-rest-high-level-client'
        exclude group: 'org.elasticsearch.client', module: 'transport'
        exclude group: 'org.elasticsearch.client', module: 'elasticsearch-rest-client'
        exclude group: 'org.elasticsearch', module: 'elasticsearch'
        exclude group: 'org.elasticsearch.plugin', module: 'transport-netty4-client'
    }
}

docker {
    registryCredentials {
        url = "${DOCKER_HUB_URL}"
        username = "${DOCKER_USERNAME}"
        password = "${DOCKER_PASSWORD}"
    }
    springBootApplication {
        maintainer = 'baihoo.chen "baihoo.chen@hotmail.com"'
        baseImage = 'swr.ap-southeast-1.myhuaweicloud.com/baihoo/oraclejdk:8u261'
        ports = [17100]
        images = ["${DOCKER_DOMAIN}/microservice-central/${archiveName}:${version}"]
        jvmArgs = ['-javaagent:/skywalking-agent/skywalking-agent.jar','-server', '-XX:MetaspaceSize=128m', '-XX:MaxMetaspaceSize=128m', '-Xms1024m', '-Xmx1024m', '-Xmn256m', '-Xss256k', '-XX:SurvivorRatio=8','-XX:+UseConcMarkSweepGC', '-XX:+PrintGCDetails']
    }
    dockerCreateDockerfile {
        environmentVariable 'SPRING_PROFILES_ACTIVE', SRPING_PROFILE
    }
}
